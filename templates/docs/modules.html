### Modules
<a name="modules"></a>
Modules allow you to seperate and organize parts of your prototype across different files. They are seperate JavaScript or CoffeeScript files, which you can include (require) within your prototype. They help you organize your code, and choose what parts to focus on. 

You can find them in the /modules folder of your prototype. Modules are based on JavaScript and Node.js standards. Everything that you've prefixed with <code class="inline">exports</code> in your module will be available in your prototype. <br> The <code class="inline">require</code> keyword imports your module.


##### Get Started 
- Create a new project in Framer Studio
- Navigate to the /modules folder and open the myModule.coffee file in a text-editor.
- To include the module within your project, add the following:

<pre lang="coffeescript">
# To include myModule.coffee in our prototype
module = require "myModule"
</pre>

Make sure to save your file and refresh your prototype to see changes. Modules can contain anything. For example, you can create Layers within your modules, or define specific interactions within functions. 

<pre lang="coffeescript">
# Store variables
exports.myVar = "myVariable"

# Create functions
exports.myFunction = -> print "I'm running!"

# Create Arrays
exports.myArray = [1, 2, 3]

# Create Layers
exports.myLayer = new Layer backgroundColor: "#fff"
</pre>

In the example above, a new layer is automatically created and included within our prototype. To also run the function, you can write:

<pre lang="coffeescript">
# To include myModule.coffee 
module = require "myModule"

# Run the function, printing "I'm running!"
module.myFunction()
</pre>


##### Example: Interactions 
<a name="modules-interactions-example"></a>

Let's create a simple draggable interaction within a module. In our myModule.coffee file, we include the following function. It takes a layer, makes it draggable, and snaps it back to its original position on DragEnd.

<pre lang="coffeescript">
# A draggable function without our module
exports.makeDraggable = (layer) ->
	layer.draggable.enabled = true

	# Store current x and y position
	startX = layer.x
	startY = layer.y

	# Animate back on DragEnd
	layer.on Events.DragEnd, ->
		this.animate
			properties: { x: startX, y: startY }
			curve: "spring(300,20,0)"
</pre>

Now, within our prototype, we can call the makeDraggable function from our module to include the dragging interaction. 

<pre lang="coffeescript">
# Include module
module = require "myModule"

# Set background
bg = new BackgroundLayer backgroundColor: "#28AFFA"

# Create a new layer
layerA = new Layer backgroundColor: "#fff", borderRadius:4
layerA.center()

# Add the dragging interaction to layerA
module.makeDraggable(layerA)
</pre>

##### Example: NPM 
<a name="modules-npm-example"></a>

Framer Studio modules work with <a href="https://www.npmjs.com" target="_blank">NPM</a>, a package manager where thousands of JavaScript packages are published. Let's import the <a href="http://backbonejs.org/" target="_blank">Backbone</a> library. Make sure you already have NPM installed.

<pre>
# In your terminal
cd ~/my-framer-project
npm install backbone
</pre>

Create a new module in /modules, and name it npm.coffee. Beware that if you name the file the same as the NPM module (like backbone.coffee), you could run into a recursive import problem.

<pre lang="coffeescript">
# File: modules/npm.coffee
exports.backbone = require "backbone"
</pre>

After a refresh, you can import underscore into your project.

<pre lang="coffeescript">
# Import the backbone module from your npm index
backbone = require("npm").backbone

# Or use this nice shortcut, which is the exact same
{backbone} = require "npm"
</pre>
